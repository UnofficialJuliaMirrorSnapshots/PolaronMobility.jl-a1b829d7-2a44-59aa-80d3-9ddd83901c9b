FILE="PCBM"

#####
# line styles for ColorBrewer Set1
# for use with qualitative/categorical data
# provides 8 easy-to-name colors
# compatible with gnuplot >=4.2
# author: Anna Schneider

# line styles
set style line 1 lt 1 lc rgb '#E41A1C' # red
set style line 2 lt 1 lc rgb '#377EB8' # blue
set style line 3 lt 1 lc rgb '#4DAF4A' # green
set style line 4 lt 1 lc rgb '#984EA3' # purple
set style line 5 lt 1 lc rgb '#FF7F00' # orange
set style line 6 lt 1 lc rgb '#FFFF33' # yellow
set style line 7 lt 1 lc rgb '#A65628' # brown
set style line 8 lt 1 lc rgb '#F781BF' # pink
#####

##### Gnuplotting xyborder.cfg
set style line 101 lc rgb '#808080' lt 1 lw 1
set border 3 front ls 101
set tics nomirror out scale 0.75
set format '%g'
#####

##### Gnuplotting grid.cfg
# Add light grid to plot
# AUTHOR: Hagen Wierstorf
set style line 102 lc rgb '#808080' lt 0 lw 1
set grid back ls 102
#####

# Ts, βreds, Kμs, Hμs, FHIPμs, vs, ws, ks, Ms, As, Bs, Cs, Fs, Taus
#  1    2     3    4     5     6   7    8  9   10  11  12  13  14

set yrange [0:100]

set terminal pngcairo size 1024,768 enhanced font 'Verdana,20'
set output FILE.'-mobility.png'

set xlabel "Temperature (K)"
set ylabel "Mobility (cm2/V/s)"
p FILE.'.dat' u 1:4 w lp title "Hellwarth mobility", '' u 1:3 w lp title "Kadanoff Mobility"

set terminal pngcairo size 1024,768 enhanced font 'Verdana,20'
set output FILE.'-variational.png'

set y2range [0:1]
set y2label "Mass / Tau (reduced units)"
set y2tics
p FILE.'.dat' u 1:4 w lp title "Hellwarth mobility", \
 '' u 1:6 w lp title "v (variational)", '' u 1:7 w lp title "w (variational)", \
 '' u 1:9 w lp axes x1y2 title "M (Effective mass)", '' u 1:14 w lp axes x1y2 title "Scattering Tau (ps)"


